jar {
	manifest {
		attributes(
			'Implementation-Title': 'SchemeEditor',
			'Implementation-Version': version
		)
	}
}

// standard configuration
getLogging().captureStandardOutput()
configurations { compile }

// add additional repositories here if you need them
repositories {
	mavenCentral()
}

// add any dependencies you want downloaded as part of the bootstrap process
dependencies {
	compile 'com.miglayout:miglayout:3.7'
	compile 'net.java.dev.glazedlists:glazedlists_java15:1.9.0'
	compile files('../../lib/itext-2.1.7-patched.jar')
	// SchemeEditor pulls stock lithology/symbol imagery from these schemes
	compile files('schemes/SE_lithologies.jar', 'schemes/SE_symbols.jar')
	compile project(":coretools")
}

// copies in the dependencies
task copyDependencies() {
	doLast {
		mkdir('lib')
		
		// download any dependencies and put them in lib/
		project.configurations?.compile?.each { dep ->
			if (!dep.name.startsWith('groovy-all')) {
				println "Copying ${dep.name} to SchemeEditor"
				ant.zip(destfile: new File('lib', dep.name)) {
					zipfileset(src: dep, excludes: "log4j*")
				}
			}
		}
	}
}

// Prepare Windows build of SchemeEditor.
// PSICAT and SchemeEditor are distributed as a single package. On
// Windows, SchemeEditor uses the same JRE as PSICAT. SchemeEditor.exe
// expects that JRE to be a dir called 'jre8' in the PSICAT.exe dir.
task packageWin(dependsOn: ':packageCheckWin') {
	doLast {
		def app = file('dist/win/SchemeEditor')
		if (app.exists()) {
			println "Found dist/win/SchemeEditor dir, copying in JARs from staging dir..."
			copy {
				into 'dist/win/SchemeEditor/lib'
				from file('staging').listFiles().findAll { it.name.endsWith('.jar') }
			}
		} else {
			println "No dist/win/SchemeEditor dir found, creating..."
			mkdir(app)

			// copy application files
			copy {
				into app
				from 'package/win'
			}

			copy {
				into 'dist/win/SchemeEditor/lib'
				from file('staging').listFiles().findAll { it.name.endsWith('.jar') }
			}
		}
	}
}

task packageMac(dependsOn: ':packageCheckMac') {
	doLast {
		def app = file('dist/mac/SchemeEditor.app')
		if (app.exists()) {
			// copy application JARs into bundle
			copy {
				println "Found dist/mac/SchemeEditor.app package, copying in JARs from tools/SchemeEditor/staging..."
				into 'dist/mac/SchemeEditor.app/Contents/Resources/Java'
				from file('staging').listFiles().findAll { it.name.endsWith('.jar') }
			}
		} else {
			println "No dist/mac/SchemeEditor.app found, building from scratch..."
			mkdir(app)

			// copy the .app bundle skeleton
			copy {
				into app
				from 'package/mac'
			}

			// copy application JARs into bundle
			copy {
				into 'dist/mac/SchemeEditor.app/Contents/Resources/Java'
				from file('staging').listFiles().findAll { it.name.endsWith('.jar') }
			}

			// update our Info.plist
			def classpath = file('staging').listFiles().findAll { it.name.endsWith('.jar') }.sort().collect { file ->
				'\t\t\t<string>\\$JAVAROOT/' + file.name + '</string>'
			}
			def plist = file('dist/mac/SchemeEditor.app/Contents/Info.plist')
			def text = plist.text.toString()
			text = text.replaceAll(':version', version) // add our version
			text = text.replaceAll(':classpath', classpath.join('\n'))
			text = text.replaceAll(':java_runtime', javaRuntimeFile)
			plist.write(text)

			// make our app executable...needed?
			ant.chmod(file:"dist/mac/SchemeEditor.app/Contents/MacOS/SchemeEditor", perm:"ugo+rx")

			// codesign the app so Mac users are only pestered by the "Do you want SchemeEditor
			// to allow incoming network connections" dialog once...otherwise it appears
			// at every launch.
			// exec {
			// 	println "Code signing app package"
			// 	workingDir 'dist/mac'
			// 	commandLine 'codesign', '--force', '--deep', '--sign', '-', 'SchemeEditor.app'
			// 	standardOutput = new ByteArrayOutputStream()
			// 	println standardOutput.toString()
			// }

			// copy Java runtime into bundle
			copy {
				from '../../package/java_runtime/mac'
				into 'dist/mac/SchemeEditor.app/Contents/JavaRuntime'
				include "${javaRuntimeFile}/**"
			}
		}
	}
}

// overwrite the tasks provided by the Groovy/Java plugin
// task clean(overwrite: true)		<< { /* griffon 'clean' */ }
// task assemble(overwrite: true)	<< { /* griffon 'compile' */ }
// task check(overwrite: true)		<< { /* griffon 'test-app' */ }
// task build(overwrite: true)		<< { /* griffon 'test-app' */ }

// additional run-related tasks
// task 'run-app'(dependsOn: init)			<< { griffon 'run-app' }
// task 'debug-app'(dependsOn: init)		<< { griffon 'run-app -debug' }
// task 'run-webstart'(dependsOn: init)	<< { griffon 'run-webstart' }
// task 'run-applet'(dependsOn: init)		<< { griffon 'run-applet' }

// packaging tasks
// task 'package'(dependsOn: [clean, init]) { griffon 'create-all-launchers' }

// call out to the griffon command
// def griffon(target) {
// 	if (System.getProperty("os.name").toLowerCase().startsWith("win")) {
// 		println "SchemeEditor: griffon.bat $target"
// 		ant.exec(executable: 'griffon.bat', dir: projectDir, failonerror: true) {
// 			arg(value: target)
// 		}	
// 	} else {
// 		println "SchemeEditor: griffon $target"
// 		ant.exec(executable: 'griffon', dir: projectDir, failonerror: true) {
// 			arg(value: target)
// 		}
// 	}
// }

// if you want to use the gradle wrapper
/*
task wrapper(type: Wrapper) << {
	gradleVersion = '0.8' 
}
*/