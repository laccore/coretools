allprojects {
	apply plugin: 'groovy'
	
	repositories {
       	mavenCentral()
	}
	
	configurations {
		all*.exclude group:'bouncycastle'
	}

	dependencies {
		implementation 'org.codehaus.groovy:groovy-all:1.6.4'
		compile 'com.google.collections:google-collections:1.0-rc2'
		compile 'com.google.inject:guice:2.0'
		compile 'com.google.inject.extensions:guice-multibindings:2.0'
		compile 'aopalliance:aopalliance:1.0'
		compile 'org.slf4j:slf4j-api:1.7.5'
		runtime 'org.slf4j:slf4j-log4j12:1.7.5'
		compile 'xerces:xerces:2.3.0' // brg 2/19/2014: needed on Windows, not on OSX for some reason?
		testCompile 'junit:junit:3.8.2'
	}

	java {
		sourceCompatibility = JavaVersion.VERSION_1_6
		targetCompatibility = JavaVersion.VERSION_1_6
	}
	version = '1.2.3'

	// Java runtime dir, required for packaging
	project.ext.javaRuntimeFile = 'jre8'
}

// Griffon 0.2 requires compilation with Java 6
final javaHome = '/Library/Java/JavaVirtualMachines/1.6.0.jdk/Contents/Home'
def javaExecutablesPath = new File(javaHome, 'bin')
def javaExecutables = [:].withDefault { execName ->
	def executable = new File(javaExecutablesPath, execName)
	assert executable.exists(): "There is no ${execName} executable in ${javaExecutablesPath}"
	executable
}
tasks.withType(AbstractCompile) {
	options.with {
		fork = true
		forkOptions.javaHome = file(javaHome)
	}
}


task packageCheckMac() {
	doLast {
		stagingDirCheck()
		def javaRuntimeDir = file("${project.rootDir}/package/java_runtime/mac")
		println "Checking for Mac Java runtime dir and JRE..."
		if (!javaRuntimeDir.exists()) {
			throw new Error("Error: javaRuntimeDir ${javaRuntimeDir.absolutePath} does not exist.")
		}
		if (javaRuntimeFile == null) {
			throw new Error("Error: javaRuntimeFile var is null.\nTo resolve, update javaRuntimeFile var with your Java 8 runtime in build.gradle.")
		}
		if (javaRuntimeDir.listFiles().find { it.name.equals(javaRuntimeFile) } == null) {
			throw new Error("Error: Missing Java runtime file '${javaRuntimeFile}' in ${javaRuntimeDir.absolutePath}")
		}
	}
}

task packageCheckWin() {
	doLast {
		stagingDirCheck()
		def javaRuntimeDir = file("${project.rootDir}/package/java_runtime/win")
		println "Checking for Windows Java runtime dir and JRE..."
		if (!javaRuntimeDir.exists()) {
			throw new Error("Error: javaRuntimeDir ${javaRuntimeDir.absolutePath} does not exist.")
		}
		if (javaRuntimeFile == null) {
			throw new Error("Error: javaRuntimeFile var is null.\nTo resolve, update javaRuntimeFile var with your Java 8 runtime in build.gradle.")
		}
		if (javaRuntimeDir.listFiles().find { it.name.equals(javaRuntimeFile) } == null) {
			throw new Error("Error: Missing Java runtime file '${javaRuntimeFile}' in ${javaRuntimeDir.absolutePath}")
		}
	}
}

// Ensure staging dir is present for PSICAT and SchemeEditor.
// The staging dir is created by griffon on the run-app command. It differs from the sibling lib
// dir in one very important way: in addition to dependencies' JARs, it contains the application's
// JAR (PSICAT.jar or SchemeEditor.jar).
//
// JARs are copied into binary packages from the staging dir. Additionally, the Mac binary's Info.plist
// classpath is built from the staging dir's contents. Without a staging dir at package-time,
// built .apps and .exes will not launch and you will be confused.
def stagingDirCheck() {
	['PSICAT', 'SchemeEditor'].each {
		def stagingDir = file("${project.rootDir}/tools/${it}/staging")
		if (!stagingDir.exists()) {
			throw new Error("Error: tools/$it/staging directory does not exist. Run $it (`griffon run-app` in tools/$it) to build this directory.")
		}
	}
}

// Package PSICAT, SchemeEditor app bundles and resource files into a bundle
// ready for Mac distribution.
task packagePSICATMac() {
	dependsOn ':tools:PSICAT:packageMac'
	dependsOn ':tools:SchemeEditor:packageMac'

	doLast {
		def app = file("dist/mac/PSICAT-${version}")
		if (!app.exists()) {
			println "${app.absolutePath} not found, creating."
		}

		// clear existing bundle to avoid failed copy of JRE contents
		delete "dist/mac/PSICAT-${version}/PSICAT.app"
		copy {
			into app
			from "tools/PSICAT/dist/mac"  // PSICAT app bundle
		}

		// clear existing bundle to avoid failed copy of JRE contents
		delete "dist/mac/PSICAT-${version}/SchemeEditor.app"
		copy {
			into app
			from "tools/SchemeEditor/dist/mac" // SchemeEditor app bundle
		}

		copy { // legacy schemes
			into new File(app, "legacy schemes")
			from "schemes/legacy"
		}

		copy { // modern schemes
			into new File(app, "resources")
			from "schemes"
			exclude "legacy"
		}

		// tar and zip up the entire package
		exec {
			workingDir "dist/mac"
			commandLine "tar", "cfvz", "PSICAT-${version}.tar.gz", "PSICAT-${version}"
		}
	}
}

// Package PSICAT, SchemeEditor executables and resource files into a bundle
// ready for Windows distribution.
task packagePSICATWin() {
	dependsOn ':tools:PSICAT:packageWin'
	dependsOn ':tools:SchemeEditor:packageWin'

	doLast {
		def app = file("dist/win/PSICAT-${version}")
		if (!app.exists()) {
			println "${app.absolutePath} not found, creating."
		}

		copy {
			into app
			from "tools/PSICAT/dist/win/PSICAT"  // PSICAT dir contents
		}

		copy {
			into app
			from "tools/SchemeEditor/dist/win" // entire SchemeEditor dir
		}

		copy { // legacy schemes
			into new File(app, "legacy schemes")
			from "schemes/legacy"
		}

		copy { // modern schemes
			into new File(app, "resources")
			from "schemes"
			exclude "legacy"
		}
	}
}

// old, likely obsolete
task 'assemble-jars'(dependsOn: build) {
	doLast {
		File distDir = new File(buildDir, 'libs')
		distDir.mkdirs()
		subprojects.findAll { it.name != 'examples' && it.name != 'scripting' }.each { project ->
			project.configurations?.compile?.files { dep -> dep.group != 'unspecified' }.each { dep ->
				ant.zip(destfile: new File(distDir, dep.name)) {
					zipfileset(src:dep, excludes:"log4j*")
				}
			}
			
			copy {
				from new File(project.buildDir, 'libs')
				into distDir
				include '*.jar'
			}
		}
	}
}